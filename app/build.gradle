plugins {
    id 'com.android.application'
}

android {
    namespace = 'edu.uiuc.cs427app'
    compileSdk 36

    defaultConfig {
        applicationId "edu.uiuc.cs427app"
        minSdk 29
        targetSdk 36
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        Properties properties = new Properties()
        if (rootProject.file("local.properties").exists()) {
            properties.load(rootProject.file("local.properties").newDataInputStream())
        }

        buildConfigField "String", "GEMINI_API_KEY_PRIMARY", "\"${properties.getProperty('GEMINI_API_KEY_PRIMARY', '')}\""
        buildConfigField "String", "GEMINI_API_KEY_BACKUP", "\"${properties.getProperty('GEMINI_API_KEY_BACKUP', '')}\""
        buildConfigField "String", "WEATHER_API_KEY", "\"${properties.getProperty('WEATHER_API_KEY', '')}\""
        manifestPlaceholders = [MAPS_API_KEY: properties.getProperty('MAPS_API_KEY', '')]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            // Enable parallel test execution
            all {
                maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
                forkEvery = 100
                testLogging {
                    events "passed", "skipped", "failed"
                }
            }
        }
    }

    lint {
        abortOnError = false
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.navigation:navigation-fragment:2.9.6'
    implementation 'androidx.navigation:navigation-ui:2.9.6'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'androidx.activity:activity:1.12.0'
    implementation 'com.squareup.okhttp3:okhttp:5.3.2'
    implementation 'com.google.ai.client.generativeai:generativeai:0.9.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'
    implementation 'com.google.guava:guava:33.5.0-android'

    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.squareup.retrofit2:converter-gson:3.0.0'
    implementation 'com.squareup.okhttp3:okhttp:5.3.2'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.json:json:20250517'
    testImplementation 'org.mockito:mockito-core:5.20.0'

    testImplementation 'org.robolectric:robolectric:4.16'
    testImplementation 'androidx.test:core:1.7.0'
    testImplementation 'androidx.test.ext:junit:1.3.0'
    testImplementation 'androidx.test.espresso:espresso-core:3.7.0'

    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation 'androidx.test:core:1.7.0'

    implementation 'com.google.android.gms:play-services-maps:18.2.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
}